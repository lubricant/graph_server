ext {
	group = 'com.soga'
	version = '0.0.1'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

allprojects { project ->

	apply plugin: 'java'
	apply plugin: 'idea'
    apply plugin: 'eclipse'
    
    repositories {
        mavenCentral()
    }
    
    configurations.all {
	    resolutionStrategy.cacheChangingModulesFor 24, 'hours'
    	resolutionStrategy.cacheDynamicVersionsFor 300, 'seconds'
    } 
    
    compileJava {
	    sourceCompatibility='1.8'
	    targetCompatibility='1.8'
	}
	
	compileTestJava {
	    sourceCompatibility='1.8'
	    targetCompatibility='1.8'
	}

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

}

subprojects { project ->

	apply plugin: 'maven'
	apply plugin: 'com.google.protobuf'

    repositories {
        mavenCentral()
        jcenter { url 'http://jcenter.bintray.com/' }
    }
    
	dependencies {
	    compile 'io.grpc:grpc-netty:1.8.0'
    	compile 'io.grpc:grpc-protobuf:1.8.0'
	    compile 'io.grpc:grpc-stub:1.8.0'
	    compile 'com.google.protobuf:protobuf-java:3.5.0'
	    compile 'com.google.guava:guava:23.0'
	    
	    compile "org.slf4j:slf4j-api:1.7.25"
	    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.10.0"

	    testCompile 'junit:junit:4.12'
	}

}

project('social-graph-api') {

	sourceSets {
        proto {
	        java {
	            srcDir 'gen/main/java'
	            srcDir 'gen/main/grpc'
	        }
            resources.srcDir 'src/main/proto'
            resources.include '**/*.proto'
        }
	}
	
	protobuf {
	    
	    protoc {
	        artifact = "com.google.protobuf:protoc:3.5.0"
	    }

	    plugins {
	        grpc {
	            artifact = 'io.grpc:protoc-gen-grpc-java:1.8.0'
	        }
	    }
		
	    generatedFilesBaseDir = "$projectDir/gen"
		generateProtoTasks {
	        all().each { task ->
	            task.plugins {
	        		grpc {}
	        	}
	        }
	    }
	}

    clean {
        delete "${protobuf.generatedFilesBaseDir}/main/java"
        delete "${protobuf.generatedFilesBaseDir}/main/grpc"
    }

	idea {
		module {
            sourceDirs += file("${projectDir}/src/main/proto")
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
        }
	}

}

project('social-graph-service') {
	dependencies {
		compile(project(':social-graph-api'))
		
		dependencies {
		    compile "com.typesafe:config:1.3.2"
		    compile 'org.apache.commons:commons-lang3:3.6'
		    compile 'org.rocksdb:rocksdbjni:5.8.6'
		}
	}
}


